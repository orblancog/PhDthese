set term postscript enhanced color 20 solid
set output 'lattice_CLIC_3TeVFD.eps'

# Set axis labels and tics and ranges
set xlabel 's [m]'
set ylabel '{/Symbol b}^{1/2}[m^{1/2}]'
set y2label '{/Symbol h}_x [mm]'
#set y2tics 0.05
set xrange [-20:]
#set xrange [0:2800]
set ytics nomirror
#set yrange [0:1400]
#set y2range [-30:40]
set y2tics 1

# Legend configuration
set key left
set key spacing 3.5

set multiplot

# twiss functions plots #

s0 = 2795.935288

set size 1.0,0.8
p 'CLIC3TeVnewbds.twiss' u ($2-s0):(sqrt($3)) w l lw 2 lt 7 t '{/Symbol b}_x^{1/2}',\
 '' u ($2-s0):(sqrt($4)) w l lw 2 lt 1 t '{/Symbol b}_y^{1/2}' , \
'' u ($2-s0):(sqrt($4)/sqrt($3)) w l lw 2 lt 2 t '{/Symbol b}_y^{1/2}/{/Symbol b}_x^{1/2}/' axes x1y2 

# Set to fit with the plot

set size 1.02,0.15
set origin -0.03,0.75

# unset plot parameters

unset xtics
unset xlabel
unset ytics
unset y2tics

#unset xrange 
set yrange [-1.3:1.3]
set xzeroaxis 
set style fill solid
unset ylabel
unset y2label

unset key
set lmargin 10
set rmargin 8
set zeroaxis

# Elements on the to plot #
# 8 -> angle
# 6 -> k1l
# 7 -> k2l

p '< grep -i SBEND CLIC3TeVnewbds.twiss' u ($2-s0):($8 != 0 ? -0.5:1/0):($10) w boxes axis x1y1 lt 3 lw 1,\
  '< grep -i SBEND CLIC3TeVnewbds.twiss' u ($2-s0):($8 != 0 ? 0.5:1/0):($10) w boxes axis x1y1 lt 3 lw 1,\
  '< grep -i QUADRUPOLE CLIC3TeVnewbds.twiss' u ($2-s0):($6/abs($6)):($10) w boxes axis x1y1 lt 1 lw 1
#  '< grep -i SEXTUPOLE CLIC3TeVnewbds.twiss' u ($2):($7/abs($7)):($10) w boxes axis x1y1 lt 0 lw 1
